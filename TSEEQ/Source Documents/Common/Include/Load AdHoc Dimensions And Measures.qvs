Trace 'Entering include file: ~\Common\include\Load AdHoc Dimensions And Measures.qvs';
/****************************************************************************************************************************************
	File:		Load AdHoc Dimensions And Measures.qvs
		
	Author:		Jeff R. Robbins
	Email:		jrs@qlik.com; jr@QlikPerf.com
	
	Version:	4.0.6
	Date:		2020-02-02
	
	This program is provided in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  
	
	Purpose:		Load Dimensions And Measures for the Native Custom Report, which provides ad hoc reporting without Edit mode.
	
	Sample Usage:  	
        
            SET vNumberOfSelectableDimensions=2;
            SET vNumberOfSelectableMeasures=2;
            $(must_Include=[$(vCommonScriptPath)\Load AdHoc Dimensions And Measures.qvs])

****************************************************************************************************************************************/
for i = 1 to vNumberOfSelectableDimensions
  
    [$(vApp)Dimensions$(i)]:
	LOAD    
        "Dimension" as %Dimension$(i),
        DimField as %SelectableDimField$(i),
	    FileName() as %DimensionSourceFile$(i)            
    FROM [$(vVariablePath)$(vApp) Dashboard Chart Dimensions.xlsx]
    (ooxml, embedded labels, table is Sheet1)
    WHERE ENABLED = 'Y' and [Lowest Dim Allowed] <= $(i);
    
    SET vDimensionCaption$(i) = if(GetPossibleCount(%Dimension$(i)) = 1, Only(%Dimension$(i)), ;
    LET vTemp= chr(39) & 'Select Dimension ' & $(i)  & chr(39) & ')';
    SET vDimensionCaption$(i)  = '$(vDimensionCaption$(i)) $(vTemp)';
next i;    

set i=;

for i = 1 to vNumberOfSelectableMeasures

    [$(vApp)Measures$(i)]:
		LOAD
        "Measure" as %Measure$(i),
        MeasureDefinition as %MeasureDefinition$(i),
        FileName() as %MeasureSourceFile$(i) 
FROM [$(vVariablePath)$(vApp) Dashboard Chart Measures.xlsx]
    (ooxml, embedded labels, table is Sheet1)
    WHERE ENABLED = 'Y';
    
    SET vMeasureCaption$(i) = if(GetPossibleCount(_Measure$(i)) = 1, Only(_Measure$(i)), ;
    LET vTemp= chr(39) & 'Select Measure' & $(i)  &  chr(39) & ')';
    SET vMeasureCaption$(i)  = '$(vMeasureCaption$(i)) $(vTemp)';  
next i

	SET i=;
    SET vTemp=;
    SET vNumberOfSelectableDimensions=;
    SET vNumberOfSelectableMeasures=;
	SET vJoinPrefix='';
	
	[$(vApp) FilterPaneFields]:
	LOAD    
        "Title" as %FilterPaneTitle,
        FilterPaneField as %FilterPaneField
        ,
	    FileName() as %FilterPaneFieldSourceFile            
    FROM [$(vVariablePath)$(vApp) Dashboard Selection Pane Fields.xlsx]
    (ooxml, embedded labels, table is Sheet1)
    WHERE ENABLED = 'Y';
	
Trace 'Exiting include file:  ~\Common\include\Load AdHoc Dimensions And Measures.qvs';